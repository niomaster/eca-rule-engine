IMPORT standaard
IMPORT simple_wordcloud
IMPORT eca_math
IMPORT eca_string

CELL 'bata_tweetlist' {'event': 'createTweetlistGadget', 'data': [json_serialize({"cell": "bata_tweetlist", "id": "bataTweetlist"})]}
CELL 'stand_tweetlist' {'event': 'createTweetlistGadget', 'data': [json_serialize({"cell": "stand_tweetlist", "id": "standTweetlist", "title": "Laatste updates tussenstand"})]}
CELL 'wordcloudcell' {'event': 'createWordCloudGadget', 'data': [json_serialize({"cell": "wordcloudcell", "id": "myWordCloud", "title": "Tweetcloud13"})]}

DECLARE tussenstand = csv_read_kv_dict('modules/tussenstand.csv',1,0)
DECLARE bata_gerelateerd = csv_read_kv_dict('modules/bata_gerelateerd.csv',1,0)
DECLARE sentiment = csv_read_kv_dict('modules/emotie.csv',1,0)
DECLARE ntweets = 0
DECLARE word_dict = {}

RULE: the_finalization_rule
EVENT: finalize
CONDITION: True
ACTION:	print("done")
	bata_gerelateerd = None
	tussenstand = None
	sentiment = None
	#word_dict = None

RULE: new_tweet_rule
EVENT: new_tweet
CONDITION: bata_gerelateerd[new_tweet.id] == "Y"
ACTION:	NEWEVENT new_bata_tweet {tweet : new_tweet}

RULE: new_bata_tweet_rule
EVENT: new_bata_tweet
CONDITION: True
ACTION:	add_tweetlist_tweet('bataTweetlist', new_bata_tweet.tweet.json_dict)

RULE: new_bata_tweet_rule_stand
EVENT: new_bata_tweet
CONDITION: tussenstand[new_bata_tweet.tweet.id] == "Y"
ACTION:	add_tweetlist_tweet('standTweetlist', new_bata_tweet.tweet.json_dict)


RULE: new_bata_tweet_rule_emotie
EVENT: new_bata_tweet
CONDITION: sentiment[new_bata_tweet.tweet.id] != "_"
ACTION:	NEWEVENT new_emotie_tweet {tweet : new_bata_tweet.tweet}
	#print('emotie')

RULE: new_emotie_tweet_rule
EVENT: new_emotie_tweet
CONDITION: True
ACTION:	ntweets += 1
	FORALL w IN getwords(new_emotie_tweet.tweet.text): NEWEVENT new_word {word: w}

RULE: new_word_rule
EVENT: new_word
CONDITION: NOT ( new_word.word IN stopwords() )
ACTION:	IF new_word.word IN word_dict THEN word_dict[new_word.word] =  word_dict[new_word.word] + 1 ELSE word_dict[new_word.word] = 1

RULE: wordcloud_updater
EVENT: new_emotie_tweet
CONDITION: (ntweets % 10) == 0
ACTION:	update_wordcloud('myWordCloud',word_dict)
	#print('hoi')
