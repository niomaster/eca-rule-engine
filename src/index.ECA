IMPORT standaard
IMPORT simple_wordcloud
IMPORT eca_math
IMPORT eca_string
IMPORT utility
IMPORT thermometer
IMPORT image
IMPORT parse_json

CELL 'bata_tweetlist' {'event': 'createTweetlistGadget', 'data': [json_serialize({"cell": "bata_tweetlist", "id": "bataTweetlist"})]}
CELL 'stand_tweetlist' {'event': 'createTweetlistGadget', 'data': [json_serialize({"cell": "stand_tweetlist", "id": "standTweetlist", "title": "Laatste updates tussenstand"})]}
CELL 'wordcloudcell' {'event': 'createWordCloudGadget', 'data': [json_serialize({"cell": "wordcloudcell", "id": "myWordCloud", "title": "Emotie cloud"})]}
CELL 'thermometer' {'event': 'createThermometerGadget', 'data': [json_serialize({"cell": "thermometercell", "id": "myThermometer", "title": "Emotiemeter"})]}
#CELL 'tweetcountcell' {'event': 'createThermometerGadget', 'data': [json_serialize({"cell": "tweetcountcell", "id": "myTweetcount", "title": "Count"})]}
CELL 'image' {'event': 'createImageGadget', 'data': [json_serialize({"cell": "imagecell", "id": "myImage", "title": "Bata pics"})]}

DECLARE tussenstand = csv_read_kv_dict('modules/tussenstand.csv',1,0)
DECLARE bata_gerelateerd = csv_read_kv_dict('modules/bata_gerelateerd.csv',1,0)
DECLARE sentiment = csv_read_kv_dict('modules/emotie.csv',1,0)
DECLARE ntweets = 0
DECLARE ntweets_emotie_loos = 0
DECLARE ntweets_emotie = 0
DECLARE ntweets_emotie_positief = 0
DECLARE ntweets_emotie_negatief = 0
DECLARE word_dict = {}

RULE: the_finalization_rule
EVENT: finalize
CONDITION: True
ACTION:	print("done")
	bata_gerelateerd = None
	tussenstand = None
	sentiment = None
	word_dict = None

RULE: new_tweet_rule
EVENT: new_tweet
CONDITION: bata_gerelateerd[new_tweet.id] == "Y"
ACTION:	NEWEVENT new_bata_tweet {tweet : new_tweet}

RULE: new_bata_tweet_rule
EVENT: new_bata_tweet
CONDITION: True
ACTION:	add_tweetlist_tweet('bataTweetlist', new_bata_tweet.tweet.json_dict)
	ntweets += 1
	#updateThermometer('myTweetcount', ntweets)

RULE: new_bata_tweet_rule
EVENT: new_bata_tweet
CONDITION: (getmediaurl(new_bata_tweet.tweet) != '') AND NOT isretweet(new_bata_tweet.tweet)
ACTION:	updateImage('myImage', getmediaurl(new_bata_tweet.tweet))

RULE: new_bata_tweet_rule_stand
EVENT: new_bata_tweet
CONDITION: tussenstand[new_bata_tweet.tweet.id] == "Y"
ACTION:	add_tweetlist_tweet('standTweetlist', new_bata_tweet.tweet.json_dict)


RULE: new_bata_tweet_rule_emotie
EVENT: new_bata_tweet
CONDITION: sentiment[new_bata_tweet.tweet.id] != "_"
ACTION:	NEWEVENT new_emotie_tweet {tweet : new_bata_tweet.tweet}

RULE: new_emotie_tweet_rule
EVENT: new_emotie_tweet
CONDITION: True
ACTION:	FORALL w IN getwords(new_emotie_tweet.tweet.text): NEWEVENT new_emotie_word {word: w}
	ntweets_emotie += 1
	updateThermometer('myThermometer', (ntweets_emotie_positief / ntweets_emotie) )

RULE: new_emotie_word_rule
EVENT: new_emotie_word
CONDITION: NOT ( new_emotie_word.word IN stopwords() )
ACTION:	IF new_emotie_word.word IN word_dict THEN word_dict[new_emotie_word.word] =  word_dict[new_emotie_word.word] + 1 ELSE word_dict[new_emotie_word.word] = 1

RULE: new_bata_tweet_rule_emotie2
EVENT: new_bata_tweet
CONDITION: sentiment[new_bata_tweet.tweet.id] == "_"
ACTION:	NEWEVENT new_emotie_loze_tweet {tweet : new_bata_tweet.tweet}

RULE: new_emotie_loze_tweet_rule
EVENT: new_emotie_loze_tweet
CONDITION: True
ACTION:	FORALL w IN getwords(new_emotie_loze_tweet.tweet.text): NEWEVENT new_emotie_loos_word {word: w}
	ntweets_emotie_loos += 1

RULE: new_emotie_loos_word_rule
EVENT: new_emotie_loos_word
CONDITION: NOT ( new_emotie_loos_word.word IN stopwords() )
ACTION:	IF new_emotie_loos_word.word IN word_dict THEN word_dict[new_emotie_loos_word.word] =  word_dict[new_emotie_loos_word.word] - 1 ELSE word_dict[new_emotie_loos_word.word] = 0

RULE: wordcloud_updater
EVENT: new_bata_tweet
CONDITION: (ntweets % 10) == 0
ACTION:	update_wordcloud('myWordCloud', filter_notgreater(word_dict, 0))

RULE: new_bata_tweet_rule_emotie_p
EVENT: new_bata_tweet
CONDITION: sentiment[new_bata_tweet.tweet.id] == "+"
ACTION:	NEWEVENT new_emotie_positief_tweet {tweet : new_bata_tweet.tweet}

RULE: new_emotie_positief_tweet_rule
EVENT: new_emotie_positief_tweet
CONDITION: True
ACTION: ntweets_emotie_positief += 1

RULE: new_bata_tweet_rule_emotie_n
EVENT: new_bata_tweet
CONDITION: sentiment[new_bata_tweet.tweet.id] == "-"
ACTION:	NEWEVENT new_emotie_negatief_tweet {tweet : new_bata_tweet.tweet}

RULE: new_emotie_negatief_tweet_rule
EVENT: new_emotie_negatief_tweet
CONDITION: True
ACTION: ntweets_emotie_negatief += 1